plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.6"
}

def versionNumber = '1.6'

group = 'com.me4502'
version = versionNumber + '-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://repo.spongepowered.org/maven/" }
}

dependencies {
    compile 'org.spongepowered:spongeapi:6.0.0-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

signing {
    sign configurations.archives
}

license {
    ext {
        year = Calendar.getInstance().get(Calendar.YEAR)
    }

    header = file('HEADER.txt')
    include '**/*.java'

    ignoreFailures = false
    strictCheck = true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.me4502'
            artifactId 'ModularFramework'
            version versionNumber
        }
    }
}

bintray {
    user = System.getProperty('BINTRAY_USER')
    key = System.getProperty('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'ModularFramework'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/me4502/ModularFramework.git'
        publicDownloadNumbers = true
        version {
            name = 'ModularFramework-' + versionNumber
            desc = name
        }
    }
}
