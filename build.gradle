plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.7.3"
}

defaultTasks 'licenseFormat', 'build'

allprojects {
    project.ext.versionNumber = '1.9.1'

    group = 'com.me4502'
    version = project.ext.versionNumber + '-SNAPSHOT'
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    signing {
        required false
        sign configurations.archives
    }

    license {
        ext {
            year = Calendar.getInstance().get(Calendar.YEAR)
        }

        header = file('../HEADER.txt')
        include '**/*.java'

        ignoreFailures = false
        strictCheck = true

        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}
